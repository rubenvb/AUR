# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname=mingw-w64-crt-svn
pkgver=5727
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('mingw-w64-gcc-base' 'mingw-w64-binutils' 'mingw-w64-headers-svn' 'mingw-w64-headers-bootstrap')
makedepends=('svn')
optdepends=()
provides=('mingw-w64-crt')
conflicts=('mingw-w64-crt')
replaces=()
backup=()
options=('!strip' '!buildflags' '!libtool' '!emptydirs')
source=()
md5sums=()

_svntrunk=https://mingw-w64.svn.sourceforge.net/svnroot/mingw-w64/trunk
_svnmod=mingw-w64-crt

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}
  msg "Connecting to SVN server...."

  if [[ -d ${_svnmod}/.svn ]]; then
    (cd $_svnmod && svn up)
  else
    svn co ${_svntrunk}/${_svnmod} --config-dir ./ ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."
  
  cd ${srcdir}
  for _target in ${_targets}; do
    msg "Building ${_target} CRT"
    if [ ${_target} == "i686-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib64 --enable-lib32"
    elif [ ${_target} == "x86_64-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib32 --enable-lib64"
    fi
    mkdir -p ${srcdir}/crt-${_target} && cd ${srcdir}/crt-${_target}
    ${srcdir}/mingw-w64-crt/configure --prefix=/usr/${_target} \
        --host=${_target} --enable-wildcard \
        ${_crt_configure_args}
    make
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_target} crt"
    cd ${srcdir}/crt-${_target}
    make DESTDIR=${pkgdir} install
  done
}

