# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname='mingw-w64-headers-svn'
pkgver=5727
pkgrel=1
pkgdesc="MinGW-w64 headers for Windows"
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=()
makedepends=('svn')
optdepends=()
provides=('mingw-w64-headers')
conflicts=('mingw-w64-headers')
replaces=()
backup=()
options=('!strip' '!libtool' '!emptydirs')
source=()
md5sums=()

_svntrunk=https://mingw-w64.svn.sourceforge.net/svnroot/mingw-w64/trunk
_svnmod=mingw-w64-headers

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}
  msg "Connecting to SVN server...."

  if [[ -d ${_svnmod}/.svn ]]; then
    (cd $_svnmod && svn up)
  else
    svn co ${_svntrunk}/${_svnmod} --config-dir ./ ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."
  
  for _target in ${_targets}; do
    msg "Configuring ${_target} headers"
    mkdir -p ${srcdir}/headers-${_target} && cd ${srcdir}/headers-${_target}
    ${srcdir}/mingw-w64-headers/configure --prefix=/usr/${_target} --enable-sdk=all --host=${_target}
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_target} headers"
    cd ${srcdir}/headers-${_target}
    make DESTDIR=${pkgdir} install
    rm ${pkgdir}/usr/${_target}/include/pthread_time.h
    rm ${pkgdir}/usr/${_target}/include/pthread_unistd.h
  done

  msg "Installing MinGW-w64 licenses"
  install -Dm644 ${srcdir}/mingw-w64-headers/ddk/readme.txt ${pkgdir}/usr/share/licenses/${pkgname}/ddk-readme.txt
  install -Dm644 ${srcdir}/mingw-w64-headers/direct-x/COPYING.LIB ${pkgdir}/usr/share/licenses/${pkgname}/direct-x-COPYING.LIB
  install -Dm644 ${srcdir}/mingw-w64-headers/direct-x/readme.txt ${pkgdir}/usr/share/licenses/${pkgname}/direct-x-readme.txt
}
