# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname='mingw-w64-headers-svn'
pkgdesc="MinGW-w64 headers for Windows"
pkgver=6298
pkgrel=1
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=()
makedepends=('svn')
optdepends=()
provides=('mingw-w64-headers')
conflicts=('mingw-w64-headers')
replaces=()
backup=()
options=('!strip' '!libtool' '!emptydirs')
source=('svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-headers')
md5sums=(SKIP)

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver() {
  cd "$SRCDEST/mingw-w64-headers"
  svnversion
}

build() {
  for _target in ${_targets}; do
    msg "Configuring ${_target} headers"
    mkdir -p ${srcdir}/headers-${_target} && cd ${srcdir}/headers-${_target}
    ${srcdir}/mingw-w64-headers/configure --prefix=/usr/${_target} --enable-sdk=all --enable-secure-api --host=${_target}
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_target} headers"
    cd ${srcdir}/headers-${_target}
    make DESTDIR=${pkgdir} install
    rm ${pkgdir}/usr/${_target}/include/pthread_signal.h
    rm ${pkgdir}/usr/${_target}/include/pthread_time.h
    rm ${pkgdir}/usr/${_target}/include/pthread_unistd.h
  done

  msg "Installing MinGW-w64 licenses"
  install -Dm644 ${srcdir}/mingw-w64-headers/ddk/readme.txt ${pkgdir}/usr/share/licenses/${pkgname}/ddk-readme.txt
  install -Dm644 ${srcdir}/mingw-w64-headers/direct-x/COPYING.LIB ${pkgdir}/usr/share/licenses/${pkgname}/direct-x-COPYING.LIB
  install -Dm644 ${srcdir}/mingw-w64-headers/direct-x/readme.txt ${pkgdir}/usr/share/licenses/${pkgname}/direct-x-readme.txt
}
