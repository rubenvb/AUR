# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname=cygwin-gcc-base
pkgver=4.5.4
pkgrel=1
pkgdesc="Cross GCC for the Cygwin cross-compiler (bootstrap)"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('cygwin-bootstrap' 'cygwin')
depends=('zlib' 'libmpc' 'ppl' 'cloog' 'cygwin-binutils')
makedepends=('libmpc' 'cloog' 'ppl')
#checkdepends=('dejagnu') # Windows executables could run on Arch through bin_mft and Wine
optdepends=()
provides=()
conflicts=('cygwin-gcc')
replaces=()
backup=()
options=('!libtool' '!emptydirs')
source=("ftp://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-core-${pkgver}.tar.bz2"
        "ftp://sources.redhat.com/pub/cygwin/release/cygwin/cygwin-1.7.16-1-src.tar.bz2")
md5sums=('ca62e442629a9a7710f5d797bf1b521c'
         'a022a762f8efc6af7af46a8e69ce6a36')

build() {
  cd ${srcdir}/gcc-${pkgver}
  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  #extract winsup stuff
  tar -xf ${srcdir}/winsup-1.7.16.tar.bz2
  cp -r src/* .
  
  mkdir -p ${srcdir}/gcc-build && cd ${srcdir}/gcc-build

  ${srcdir}/gcc-${pkgver}/configure --prefix=/usr \
      --target=i686-pc-cygwin --with-newlib \
      --enable-languages=c,lto \
      --enable-shared --enable-static \
      --enable-threads=posix \
      --with-system-zlib --with-ppl --enable-cloog-backend=isl \
      --enable-lto --enable-dwarf2 --disable-sjlj-exceptions --enable-libgomp \
      --disable-nls \
      --enable-checking=release
  make all-gcc
}

package() {
  cd ${srcdir}/gcc-build
  make DESTDIR=${pkgdir} install-gcc
  # remove unnecessary files
  msg "Removing man and info pages"
  rm -r ${pkgdir}/usr/share/man
  rm -r ${pkgdir}/usr/share/info
}