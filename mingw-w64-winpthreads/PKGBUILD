# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname=mingw-w64-winpthreads-svn
pkgver=4590
pkgrel=1
pkgdesc='MinGW-w64 winpthreads library'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('mingw-w64-headers')
makedepends=('svn' 'mingw-w64-gcc-base' 'mingw-w64-binutils' 'mingw-w64-crt')
optdepends=()
provides=('mingw-w64-winpthreads')
conflicts=('mingw-w64-headers-base')
replaces=()
backup=()
options=('!strip' '!buildflags' '!libtool' '!emptydirs')
source=()
md5sums=()

_svntrunk=https://mingw-w64.svn.sourceforge.net/svnroot/mingw-w64/experimental
_svnmod=winpthreads

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}
  msg "Connecting to SVN server...."

  if [[ -d ${_svnmod}/.svn ]]; then
    (cd $_svnmod && svn up)
  else
    svn co ${_svntrunk}/${_svnmod} --config-dir ./ ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  for _target in ${_targets}; do
    mkdir -p ${srcdir}/winpthreads-build-${_target} && cd ${srcdir}/winpthreads-build-${_target}
    ${srcdir}/winpthreads/configure --prefix=/usr/${_target} \
        --host=${_target} --enable-static --enable-shared
    make
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/winpthreads-build-${_target}
    make DESTDIR=${pkgdir} install
  done
}
